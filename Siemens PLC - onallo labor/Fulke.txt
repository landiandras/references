//states:
//0: default
//1: idle with doors closed
//2: opening doors
//3: opendoors, waiting to close
//4: closing doors
//5: going down
//6: going up
//7: arrived, opening door
//8: arrived, door opened
//20: idling with doors opened
//21: idling, closing doors


#idlewait(IN:=(#state = 20 AND #las),
          PT:=T#20s);
#closedoorwait(IN:=((#state = 3) AND #las),
               PT:=T#5s);




CASE #state OF
    0:
        IF #currentlevel = 0 AND #doorclosedsens THEN
            #state := 21;
        END_IF;
    1:
        IF NOT(#currentlevel = #nextlevel) THEN
            #state := 3;
        END_IF;
    2:
        IF #dooropensens THEN
            #state := 3;
        END_IF;
           
            
    3:
        IF #closedoorwait.Q OR #closethedoorbutton THEN
            #state := 4;
        END_IF;
    4:
        IF #doorclosedsens AND #currentlevel>#nextlevel THEN
            #state := 5;
        ELSIF #doorclosedsens AND #currentlevel<#nextlevel THEN
            #state := 6;
        ELSIF #doorclosedsens AND #currentlevel=#nextlevel THEN
            #state := 7;
        ELSIF NOT #las THEN
            #state := 3;
        END_IF;
    5,6:
        IF #currentlevel=#nextlevel THEN
            #state := 7;
        END_IF;
    7:
        IF #dooropensens THEN
            #state := 8;
        END_IF;
    8: IF #ack THEN
            #callcompleted := FALSE;
            #state := 20;
            END_IF;
        #callcompleted := TRUE;
        
        
    20:
        IF NOT(#currentlevel = #nextlevel) THEN
            #state := 3;
        ELSIF (#idlewait.Q OR #closethedoorbutton) THEN
            #state := 21;
        END_IF;
    21:
        IF #stopidling THEN
            #state := 7;
        ELSIF NOT(#currentlevel = #nextlevel) THEN
            IF #doorclosedsens AND #currentlevel > #nextlevel THEN
                #state := 5;
            ELSIF #doorclosedsens AND #currentlevel < #nextlevel THEN
                #state := 6;
            END_IF;
        ELSIF #openthedoorbutton THEN
            #state := 20;
        END_IF;
    ELSE
        #state := 0;
        
END_CASE;

#callcompleted := (#state = 8 OR #state=3);
#headingup := (#state = 6);
#headingdown := (#state = 5) OR (#state = 0);
#closedoor := (#state = 4) OR (#state = 21) OR (#state=0) OR (#state=1) OR (#state=5) OR (#state=6);
#opendoor := (#state = 2) OR (#state = 7) OR (#state=3) OR (#state=20) OR (#state=8);
#idling := #state = 21;
